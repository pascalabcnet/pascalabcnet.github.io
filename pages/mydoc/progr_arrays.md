---
title: –ü—Ä–æ–≥—Ä–∞–º–º—ã –∏ –∞–ª–≥–æ—Ä–∏—Ç–º—ã —Å –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏–µ–º –º–∞—Å—Å–∏–≤–æ–≤
keywords: arrays, examples, programs, algorithms
last_updated: 31.12.19
summary: "–ó–¥–µ—Å—å –ø—Ä–∏–≤–µ–¥–µ–Ω—ã –ø—Ä–∏–º–µ—Ä—ã –ø—Ä–æ–≥—Ä–∞–º–º –∏ –∞–ª–≥–æ—Ä–∏—Ç–º–æ–≤, –∏—Å–ø–æ–ª—å–∑—É–µ–º—ã—Ö –≤ –∫—É—Ä—Å–µ –û—Å–Ω–æ–≤—ã –ø—Ä–æ–≥—Ä–∞–º–º–∏—Ä–æ–≤–∞–Ω–∏—è –¥–ª—è —Å—Ç—É–¥–µ–Ω—Ç–æ–≤ 1 –∫—É—Ä—Å–∞ –§–ò–ò–¢ –º–µ—Ö–º–∞—Ç–∞ –Æ–§–£"
sidebar: mydoc_sidebar
permalink: progr_arrays.html
folder: mydoc
---

## –ú–µ—Ç–æ–¥—ã –∏ –æ–ø–µ—Ä–∞—Ü–∏–∏ –¥–ª—è —Ä–∞–±–æ—Ç—ã —Å –º–∞—Å—Å–∏–≤–∞–º–∏

–í–≤–æ–¥-–≤—ã–≤–æ–¥
```pascal
  var a := ReadArrInteger(n);
  var r := ReadArrReal(n);
  Print(a);
  a.Println;
  a.Println(', ');
```

–ó–∞–ø–æ–ª–Ω–µ–Ω–∏–µ
```pascal
  a := Arr(1,3,7);
  a := Arr(1..10);
  a := ArrFill(10,666);
  a := ArrGen(n,i->i*i[,from:=0])
  a := ArrGen(n,1,x->x+2)
  a := ArrGen(n,1,1,(x,y)->x+y)
```

–ó–∞–ø–æ–ª–Ω–µ–Ω–∏–µ —Å–ª—É—á–∞–π–Ω—ã–º–∏ —á–∏—Å–ª–∞–º–∏
```pascal
  a := ArrRandomInteger(n[,from,to]);
  r := ArrRandomReal(n[,from,to]);
```

–°—Ä–µ–∑—ã
```pascal
  var a := Arr(0,1,2,3,4,5,6,7,8,9);
  a[2:5]    // 2 3 4 
  a[:2]     // 0 1
  a[5:]     // 5 6 7 8 9 
  a[:]      // –∫–æ–ø–∏—è –º–∞—Å—Å–∏–≤–∞ a
  a[::2]    // 0 2 4 6 8
  a[1::2]   // 1 3 5 7 9
  a[5:2:-1] // 5 4 3
  a[::-1]   // 9 8 7 6 5 4 3 2 1 0
```

–û–ø–µ—Ä–∞—Ü–∏–∏ + –∏ *
```pascal
  var a := Arr(1,2,3);
  var b := Arr(4,5,6);
  a + b // 1 2 3 4 5 6
  a * 2 // 1 2 3 1 2 3
  Arr(0)*3 + Arr(1)*3 // 0 0 0 1 1 1 
  (Arr(0) + Arr(1))*3 // 0 1 0 1 0 1
```

## –¶–∏–∫–ª—ã –ø–æ –º–∞—Å—Å–∏–≤—É

**For**
```pascal
for var i:=0 to a.Length-1 do
  a[i] += 1;
```

**Foreach**
```pascal
foreach var x in a do
  Print(x);
```

**Foreach –ø–æ –∏–Ω–¥–µ–∫—Å–∞–º**
```pascal
foreach var i in a.Indices do
  a[i] *= 2;
```

**Foreach –ø–æ –æ–ø—Ä–µ–¥–µ–ª—ë–Ω–Ω—ã–º –∏–Ω–¥–µ–∫—Å–∞–º**
```pascal
foreach var i in a.Indices(x -> x in 10..20) do
  a[i] += 1;
```

```pascal
foreach var i in a.Indices((x,i) -> i.IsEven and (x > 0)) do
  a[i] += 1;
```

## –ò–Ω–≤–µ—Ä—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ

**–ó–∞–¥–∞—á–∞.** –ò–Ω–≤–µ—Ä—Ç–∏—Ä–æ–≤–∞—Ç—å –º–∞—Å—Å–∏–≤

**–†–µ—à–µ–Ω–∏–µ 1.** –ê–ª–≥–æ—Ä–∏—Ç–º
```pascal
procedure Reverse<T>(a: array of T);
begin
  var n := a.Length;
  for var i:=0 to n div 2 - 1 do
    Swap(a[i],a[n-i-1]);
end;
```

**–†–µ—à–µ–Ω–∏–µ 2.** –° –ø–æ–º–æ—â—å—é —Å—Ä–µ–∑–æ–≤
```pascal
a := a[::-1]
```

**–†–µ—à–µ–Ω–∏–µ 3.** –° –ø–æ–º–æ—â—å—é —Å—Ç–∞–Ω–¥–∞—Ä—Ç–Ω–æ–π –ø—Ä–æ—Ü–µ–¥—É—Ä—ã
```pascal
Reverse(a)
```

## –ü–æ–∏—Å–∫

**–ó–∞–¥–∞—á–∞.** –ï—Å—Ç—å –ª–∏ –≤ –º–∞—Å—Å–∏–≤–µ a —ç–ª–µ–º–µ–Ω—Ç x

**–†–µ—à–µ–Ω–∏–µ.** –° –ø–æ–º–æ—â—å—é –æ–ø–µ—Ä–∞—Ü–∏–∏ in –∏–ª–∏ –º–µ—Ç–æ–¥–∞ Contains:
```pascal
x in a
a.Contains(x)
```

**–ó–∞–¥–∞—á–∞.** –ù–∞–π—Ç–∏ –∏–Ω–¥–µ–∫—Å –ø–µ—Ä–≤–æ–≥–æ –≤—Ö–æ–∂–¥–µ–Ω–∏—è —ç–ª–µ–º–µ–Ω—Ç–∞ x

**–†–µ—à–µ–Ω–∏–µ 1.** –° –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏–µ–º break
```pascal
function IndexOf<T>(a: array of T; x: T): integer;
begin
  Result := -1;
  for var i := 0 to a.High do // a.High = a.Length - 1
    if a[i] = x then
    begin
      Result := i;
      break;
    end;
end;
```

**–†–µ—à–µ–Ω–∏–µ 2.** –ë–µ–∑ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏—è break
```pascal
function IndexOfW<T>(a: array of T; x: T): integer;
begin
  var n := a.Length;
  var i := 0;
  while (i<n) and (a[i]<>x) do
    i += 1;
  Result := i=n ? -1 : i;
end;
```

**–†–µ—à–µ–Ω–∏–µ 3.** –ü–æ–∏—Å–∫ —Å –±–∞—Ä—å–µ—Ä–æ–º

–î–æ–±–∞–≤–∏–º –≤ –∫–æ–Ω–µ—Ü –º–∞—Å—Å–∏–≤–∞ –±–∞—Ä—å–µ—Ä, —Ä–∞–≤–Ω—ã–π x. –í –º–∞—Å—Å–∏–≤–µ –¥–æ–ª–∂–Ω–æ –±—ã—Ç—å –º–µ—Å—Ç–æ –ø–æ–¥ —ç—Ç–æ—Ç —ç–ª–µ–º–µ–Ω—Ç

```pascal
function IndexOfWithBarrier<T>
  (a: array of T; n: integer; x: T): integer;
begin
  Assert((0 < n) and (n < a.Length));
  a[n] := x; // –±–∞—Ä—å–µ—Ä
  var i := 0;
  while a[i]<>x do
    i += 1;
  Result := i=n ? -1 : i;
end;
```

–ó–∞ —Å—á–µ—Ç –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏—è –±–∞—Ä—å–µ—Ä–∞ —ç–∫–æ–Ω–æ–º–∏—Ç—Å—è –æ–¥–Ω–∞ –æ–ø–µ—Ä–∞—Ü–∏—è —Å—Ä–∞–≤–Ω–µ–Ω–∏—è

**–†–µ—à–µ–Ω–∏–µ 4.** –°—Ç–∞–Ω–¥–∞—Ä—Ç–Ω—ã–µ –º–µ—Ç–æ–¥—ã
```pascal
a.IndexOf(x)
a.LastIndexOf(x)
```


## –ü–æ–∏—Å–∫ –ø–æ —É—Å–ª–æ–≤–∏—é

**–ó–∞–¥–∞—á–∞.** –ü–æ–∏—Å–∫ –ø–æ —É—Å–ª–æ–≤–∏—é 

**–†–µ—à–µ–Ω–∏–µ 1.** –ê–ª–≥–æ—Ä–∏—Ç–º
```pascal
function FindIndex<T>(a: array of T; cond: T->boolean): integer;
begin
  Result := -1;
  for var i := 0 to a.High do
    if cond(a[i]) then
    begin
      Result := i;
      break;
    end;
end;
```

**–†–µ—à–µ–Ω–∏–µ 2.** –° –ø–æ–º–æ—â—å—é —Å—Ç–∞–Ω–¥–∞—Ä—Ç–Ω–æ–π —Ñ—É–Ω–∫—Ü–∏–∏
```pascal
a.FindIndex(cond)
```

## –ö–æ–ª–∏—á–µ—Å—Ç–≤–æ –ø–æ —É—Å–ª–æ–≤–∏—é

**–ó–∞–¥–∞—á–∞.** –ö–æ–ª–∏—á–µ—Å—Ç–≤–æ —ç–ª–µ–º–µ–Ω—Ç–æ–≤, —É–¥–æ–≤–ª–µ—Ç–≤–æ—Ä—è—é—â–∏—Ö –∑–∞–¥–∞–Ω–Ω–æ–º—É —É—Å–ª–æ–≤–∏—é

**–†–µ—à–µ–Ω–∏–µ 1.** –ê–ª–≥–æ—Ä–∏—Ç–º

```pascal
function Count<T>(a: array of T; cond: T->boolean): integer;
begin
  Result := 0;
  foreach var x in a do
    if cond(x) then
      Result += 1;
end;
```

**–†–µ—à–µ–Ω–∏–µ 2.** –° –ø–æ–º–æ—â—å—é —Å—Ç–∞–Ω–¥–∞—Ä—Ç–Ω–æ–π —Ñ—É–Ω–∫—Ü–∏–∏
```pascal
a.Count(—É—Å–ª–æ–≤–∏–µ)
```

## –ú–∏–Ω–∏–º—É–º—ã-–º–∞–∫—Å–∏–º—É–º—ã

**–ó–∞–¥–∞—á–∞.** –ù–∞–π—Ç–∏ –º–∏–Ω–∏–º–∞–ª—å–Ω—ã–π —ç–ª–µ–º–µ–Ω—Ç –∏ –µ–≥–æ –∏–Ω–¥–µ–∫—Å 

**–†–µ—à–µ–Ω–∏–µ 1.** –ê–ª–≥–æ—Ä–∏—Ç–º
```pascal
function MinElemAndIndex(a: array of real): (real,integer); begin
  var (min, minind) := (real.MaxValue, -1);  
  for var i:=0 to a.Length-1 do
    if a[i]<min then
      (min, minind) := (a[i], i);  Result := (min, minind)
end;
```

**–†–µ—à–µ–Ω–∏–µ 2.** –° –ø–æ–º–æ—â—å—é —Å—Ç–∞–Ω–¥–∞—Ä—Ç–Ω–æ–π —Ñ—É–Ω–∫—Ü–∏–∏
```pascal
a.Min
a.IndexMin
```

**–ó–∞–¥–∞—á–∞.** –ù–∞–π—Ç–∏ –º–∏–Ω–∏–º–∞–ª—å–Ω—ã–π —ç–ª–µ–º–µ–Ω—Ç, —É–¥–æ–≤–ª–µ—Ç–≤–æ—Ä—è—é—â–∏–π —É—Å–ª–æ–≤–∏—é, –∏ –µ–≥–æ –∏–Ω–¥–µ–∫—Å 

**–†–µ—à–µ–Ω–∏–µ.** –ê–ª–≥–æ—Ä–∏—Ç–º
```pascal
function MinElemAndIndexCond(a: array of real: cond: real -> boolean): 
  (real,integer); 
begin
  var (min, minind) := (real.MaxValue, -1);  
  for var i:=0 to a.Length-1 do
    if (a[i]<min) and cond(a[i]) then
      (min, minind) := (a[i], i);
  Result := (min, minind)
end;
```


## –°–¥–≤–∏–≥–∏

**–ó–∞–¥–∞—á–∞.** –°–¥–≤–∏–≥ –≤–ª–µ–≤–æ –Ω–∞ 1

**–†–µ—à–µ–Ω–∏–µ 1.** –ê–ª–≥–æ—Ä–∏—Ç–º
```pascal
procedure ShiftLeft<T>(a: array of T);
begin
  for var i:=0 to a.Length-2 do
    a[i] := a[i+1];
  a[a.Length-1] := default(T);
end;
```

**–†–µ—à–µ–Ω–∏–µ 2.** –° –ø–æ–º–æ—â—å—é —Å—Ä–µ–∑–æ–≤
```pascal
a := a[1:] + Arr(0);
```

**–ó–∞–¥–∞—á–∞.** –°–¥–≤–∏–≥ –≤–ø—Ä–∞–≤–æ

**–†–µ—à–µ–Ω–∏–µ 1.** –ê–ª–≥–æ—Ä–∏—Ç–º
```pascal
procedure ShiftRight<T>(a: array of T);
begin
  for var i:=a.Length-1 downto 1 do
    a[i] := a[i-1];
  a[0] := default(T);
end;
```

**–†–µ—à–µ–Ω–∏–µ 2.** –° –ø–æ–º–æ—â—å—é —Å—Ä–µ–∑–æ–≤
```pascal
a := Arr(0) + a[:a.Length-1];
```

**–ó–∞–¥–∞—á–∞.** –¶–∏–∫–ª–∏—á–µ—Å–∫–∏–π —Å–¥–≤–∏–≥ –≤–ø—Ä–∞–≤–æ

**–†–µ—à–µ–Ω–∏–µ 1.** –ê–ª–≥–æ—Ä–∏—Ç–º
```pascal
procedure CycleShiftRight<T>(a: array of T);
begin
  var v := a.Last;
  for var i:=a.Length-1 downto 1 do
    a[i] := a[i-1];
  a[0] := v;  
end;
```

**–†–µ—à–µ–Ω–∏–µ 2.** –° –ø–æ–º–æ—â—å—é —Å—Ä–µ–∑–æ–≤
```pascal
var m := a.Length-1;
a := a[m:] + a[:m];
```

**–ó–∞–¥–∞—á–∞.** –¶–∏–∫–ª–∏—á–µ—Å–∫–∏–π —Å–¥–≤–∏–≥ –≤–ª–µ–≤–æ –Ω–∞ k

**–†–µ—à–µ–Ω–∏–µ 1.** –° –ø–æ–º–æ—â—å—é —Å—Ä–µ–∑–æ–≤
```pascal
a := a[k:] + a[:k];
```

**–†–µ—à–µ–Ω–∏–µ 2.** –° –ø–æ–º–æ—â—å—é —á–∞—Å—Ç–∏—á–Ω–æ–≥–æ Reverse
```pascal
Reverse(a,0,k);
Reverse(a,k,a.Length-k);
Reverse(a);
```

## –ü—Ä–µ–æ–±—Ä–∞–∑–æ–≤–∞–Ω–∏–µ —ç–ª–µ–º–µ–Ω—Ç–æ–≤

**–ó–∞–¥–∞—á–∞.** –¢—Ä–µ–±—É–µ—Ç—Å—è –ø—Ä–µ–æ–±—Ä–∞–∑–æ–≤–∞—Ç—å —ç–ª–µ–º–µ–Ω—Ç—ã –º–∞—Å—Å–∏–≤–∞ –ø–æ –ø—Ä–∞–≤–∏–ª—É x -> f(x)

**–†–µ—à–µ–Ω–∏–µ 1.** –ê–ª–≥–æ—Ä–∏—Ç–º
```pascal
procedure Transform<T>(a: array of T; f: T -> T);
begin
  for var i:=0 to a.Length-1 do
    a[i] := f(a[i]);
end;
```

**–†–µ—à–µ–Ω–∏–µ 2.** –° –ø–æ–º–æ—â—å—é —Å—Ç–∞–Ω–¥–∞—Ä—Ç–Ω–æ–π —Ñ—É–Ω–∫—Ü–∏–∏
```pascal
a.Transform(x -> x*x)
```

–î–ª—è –ø—Ä–µ–æ–±—Ä–∞–∑–æ–≤–∞–Ω–∏—è —á–∞—Å—Ç–∏ —ç–ª–µ–º–µ–Ω—Ç–æ–≤:

```pascal
a.Transform(x -> if x mod 2 = 0 then x*x else x)
```

## –°–ª–∏—è–Ω–∏–µ

**–ó–∞–¥–∞—á–∞.** –°–ª–∏—è–Ω–∏–µ –¥–≤—É—Ö —É–ø–æ—Ä—è–¥–æ—á–µ–Ω–Ω—ã—Ö –º–∞—Å—Å–∏–≤–æ–≤ –≤ –æ–¥–∏–Ω —É–ø–æ—Ä—è–¥–æ—á–µ–Ω–Ω—ã–π

–í –º–∞—Å—Å–∏–≤–∞—Ö –¥–æ–ª–∂–Ω–æ –±—ã—Ç—å –º–µ—Å—Ç–æ –ø–æ–¥ –æ–¥–∏–Ω –±–∞—Ä—å–µ—Ä–Ω—ã–π —ç–ª–º–µ–Ω—Ç

```pascal
function Merge(a,b: array of real; n,m: integer): 
  array of real;
begin
  Assert((0 < n) and (n < a.Length));
  Assert((0 < m) and (m < b.Length));
  a[n] := real.MaxValue; // –±–∞—Ä—å–µ—Ä
  b[m] := real.MaxValue; // –±–∞—Ä—å–µ—Ä
  SetLength(Result,m+n);
  var (ia,ib) := (0,0);
  for var ir:=0 to n+m-1 do
    if a[ia]<b[ib] then
    begin
      Result[ir] := a[ia]; 
      ia += 1;
    end
    else
    begin
      Result[ir] := b[ib]; 
      ib += 1;
    end;
end;
```

## –ë–∏–Ω–∞—Ä–Ω—ã–π –ø–æ–∏—Å–∫

**–ó–∞–¥–∞—á–∞.** –ü–æ–∏—Å–∫ –≤ —É–ø–æ—Ä—è–¥–æ—á–µ–Ω–Ω–æ–º –º–∞—Å—Å–∏–≤–µ (–µ—Å—Ç—å —Å—Ç–∞–Ω–¥–∞—Ä—Ç–Ω–∞—è a.BinarySearch(x))
```pascal
function BinarySearch(a: array of integer; x: integer): integer;
begin
  var k: integer;
  var (l,r) := (0, a.Length-1); 
  repeat
    k := (l+r) div 2;
    if x>a[k] then
      l := k+1
    else r := k-1;
  until (a[k]=x) or (l>r);
  Result := a[k]=x ? k : -1;
end;
```

–ê—Å–∏–º–ø—Ç–æ—Ç–∏—á–µ—Å–∫–∞—è —Å–ª–æ–∂–Ω–æ—Å—Ç—å - O(log(n))

## –ê–ª–≥–æ—Ä–∏—Ç–º—ã —Å–æ—Ä—Ç–∏—Ä–æ–≤–∫–∏

### –°–æ—Ä—Ç–∏—Ä–æ–≤–∫–∞ –≤—ã–±–æ—Ä–æ–º

```pascal
procedure SortByChoice(a: array of integer);
begin
  for var i := 0 to a.High-1 do
  begin
    var min := a[i];
    var imin := i;
    for var j := i + 1 to a.High do
      if a[j] < min then
      begin        
        min := a[j];
        imin := j;
      end;  
    Swap(a[imin],a[i]);
  end;
end;
```

–° –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏–µ–º —Å—Ä–µ–∑–æ–≤:

```pascal
procedure SortByChoice(a: array of integer);
begin
  for var i := 0 to a.High-1 do
    Swap(a[a[i:].IndexMin + i],a[i]);
end;
```

## –ü—É–∑—ã—Ä—å–∫–æ–≤–∞—è —Å–æ—Ä—Ç–∏—Ä–æ–≤–∫–∞ 

```pascal
procedure BubbleSort(a: array of integer);
begin
  for var i := 0 to a.High-1 do
    for var j := a.High downto i+1 do
      if a[j] < a[j-1] then
        Swap(a[j], a[j-1]);
end;
```

–° —Ñ–ª–∞–≥–æ–º (—ç—Ñ—Ñ–µ–∫—Ç–∏–≤–Ω–µ–µ –≤ —Å–∏—Ç—É–∞—Ü–∏—è—Ö, –∫–æ–≥–¥–∞ –º–∞—Å—Å–∏–≤ —á–∞—Å—Ç–∏—á–Ω–æ –æ—Ç—Å–æ—Ä—Ç–∏—Ä–æ–≤–∞–Ω):

```pascal
procedure BubbleSort2(a: array of integer);
begin
  var i := a.High;
  var q: boolean;
  repeat
    q := true;
    for var j := 0 to i - 1 do
      if a[j+1] < a[j] then
      begin
        Swap(a[j+1], a[j]);
        q := false;
      end;
    i -= 1;
  until q;
end;
```

## –°–æ—Ä—Ç–∏—Ä–æ–≤–∫–∞ –≤—Å—Ç–∞–≤–∫–∞–º–∏

```pascal
procedure SortByInsert(a: array of integer);
begin
  for var i:=1 to a.High do
  begin
    var x := a[i];
    var j := i - 1;
    while (j >= 0) and (x < a[j]) do
    begin
      a[j+1] := a[j];
      j -= 1;
    end;
    a[j+1] := x;
  end;
end;
```

## –°—Ç–∞–Ω–¥–∞—Ä—Ç–Ω–∞—è —Å–æ—Ä—Ç–∏—Ä–æ–≤–∫–∞
```pascal
Sort(a);
```

–ê—Å–∏–º–ø—Ç–æ—Ç–∏—á–µ—Å–∫–∞—è —Å–ª–æ–∂–Ω–æ—Å—Ç—å ‚Äì Œò(ùëõ‚àôlog‚Å°(ùëõ))

{% include links.html %}
